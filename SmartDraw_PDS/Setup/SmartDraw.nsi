; Script generated by the HM NIS Edit Script Wizard.

!system "GetVersion.exe SmartDraw.exe"
!include "VersionNo.nsi"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "SmartDraw_PDS"
!define PRODUCT_PUBLISHER "TechSun"
!define PRODUCT_WEB_SITE "http://www.SolutionWare.co.kr/SmartDraw"
!define PRODUCT_DIR_REGKEY "Software\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}"
!define PRODUCT_UNINST_KEY "Software\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}\Uninstall"
!define PRODUCT_UNINST_ROOT_KEY "hklm"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "TechSun.bmp" ; optional
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Component page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY

; MSTN J폴더를 선택하는 페이지
Var vMSTNJFolder
Var vMSTNV8iFolder
Var vAutoCAD2006Folder
!define TEMP1 $R0 ;Temp variable
Page custom SmartDraw_NSIS SmartDraw_NSISLeave

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\SmartDraw.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Korean"
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_RESERVEFILE_LANGDLL

BrandingText "SmartDraw"
; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Installation\SmartDraw_PDS.${PRODUCT_VERSION}.exe"

InstallDir "$PROGRAMFILES\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Reverse a string
;; P1 :o: Reversed string
;; P2 :i: Original string
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
!define StrRev "!insertmacro _StrRev"
!macro _StrRev _REV_ _STR_
   Push "${_STR_}"
   Call StrRev
   Pop ${_REV_}
!macroend

Function StrRev
   Exch $1  ;; Orig string
   Push $0  ;; Reversed string
   Exch
   Push $2  ;; String Len / Counter
   Push $3  ;; Current character

   StrCpy $0 ""
   StrLen $2 "$1"

   IntCmp $2 0 +5
      IntOp $2 $2 - 1
      StrCpy $3 "$1" 1 $2
      StrCpy $0 "$0$3"
      Goto -4

   Pop $3
   Pop $2
   Pop $1
   Exch $0
FunctionEnd

!define StrLoc "!insertmacro StrLoc"

!macro StrLoc ResultVar String SubString StartPoint
  Push "${String}"
  Push "${SubString}"
  Push "${StartPoint}"
  Call StrLoc
  Pop "${ResultVar}"
!macroend

Function StrLoc
/*After this point:
  ------------------------------------------
   $R0 = StartPoint (input)
   $R1 = SubString (input)
   $R2 = String (input)
   $R3 = SubStringLen (temp)
   $R4 = StrLen (temp)
   $R5 = StartCharPos (temp)
   $R6 = TempStr (temp)*/

  ;Get input from user
  Exch $R0
  Exch
  Exch $R1
  Exch 2
  Exch $R2
  Push $R3
  Push $R4
  Push $R5
  Push $R6

  ;Get "String" and "SubString" length
  StrLen $R3 $R1
  StrLen $R4 $R2
  ;Start "StartCharPos" counter
  StrCpy $R5 0

  ;Loop until "SubString" is found or "String" reaches its end
  ${Do}
    ;Remove everything before and after the searched part ("TempStr")
    StrCpy $R6 $R2 $R3 $R5

    ;Compare "TempStr" with "SubString"
    ${If} $R6 == $R1
      ${If} $R0 == `<`
        IntOp $R6 $R3 + $R5
        IntOp $R0 $R4 - $R6
      ${Else}
        StrCpy $R0 $R5
      ${EndIf}
      ${ExitDo}
    ${EndIf}
    ;If not "SubString", this could be "String"'s end
    ${If} $R5 >= $R4
      StrCpy $R0 ``
      ${ExitDo}
    ${EndIf}
    ;If not, continue the loop
    IntOp $R5 $R5 + 1
  ${Loop}

  ;Return output to user
  Pop $R6
  Pop $R5
  Pop $R4
  Pop $R3
  Pop $R2
  Exch
  Pop $R1
  Exch $R0
FunctionEnd

Function SmartDraw_NSIS
Push ${TEMP1}
    SetOutPath "$INSTDIR"
    File /oname=$INSTDIR\SmartDraw_NSIS.ini "SmartDraw_NSIS.ini"

    !insertmacro MUI_HEADER_TEXT 'Write Module' 'MSTN or AutoCAD 설치 폴더를 선택하세요'

    ; get microstation j folder
    ReadRegStr $1 HKLM SOFTWARE\Bentley\MicroStation\07.01 "PathName"
    ${StrRev} $0 "$1"   ;; $StrVar="gnirtS"
    ${StrLoc} $R0 $0 '\' '<'
    StrCpy $0 "$1" $R0 # get v8i folder
    WriteINIStr "$INSTDIR\SmartDraw_NSIS.ini" "Field 4" "State" "$0"
    
    ; get v8i folder
    ReadRegStr $1 HKLM SOFTWARE\Bentley\MicroStation\08.11.07 "PathName"     # V8i SelectSeries2
    ${If} $1 == ""
        ReadRegStr $1 HKLM SOFTWARE\Bentley\MicroStation\08.09 "PathName"    # V8 XM edition
    ${EndIf}
    ${StrRev} $0 "$1"   ;; $StrVar="gnirtS"
    ${StrLoc} $R0 $0 '\' '<'
    StrCpy $0 "$1" $R0 # get v8i folder
    WriteINIStr "$INSTDIR\SmartDraw_NSIS.ini" "Field 5" "State" "$0"

    ; get AutoCAD2010 folder
    ReadRegStr $1 HKLM SOFTWARE\Autodesk\AutoCAD\R18.0\ACAD-8001:412 "AcadLocation"     # AutoCAD2010 location
    WriteINIStr "$INSTDIR\SmartDraw_NSIS.ini" "Field 6" "State" "$1"
    
    InstallOptions::dialog "$INSTDIR\SmartDraw_NSIS.ini"
    Pop ${TEMP1}

  Pop ${TEMP1}
FunctionEnd

Function SmartDraw_NSISLeave
    ReadINIStr $vMSTNJFolder "$INSTDIR\SmartDraw_NSIS.ini" "Field 4" "State"
    ReadINIStr $vMSTNV8iFolder "$INSTDIR\SmartDraw_NSIS.ini" "Field 5" "State"
    ReadINIStr $vAutoCAD2006Folder "$INSTDIR\SmartDraw_NSIS.ini" "Field 6" "State"
         
    ;copy V7 ma files file to mdlapps folder
    ${If} $vMSTNJFolder != ""
        File /oname=$vMSTNJFolder\mdlapps\aDgnWriter.ma  "Backup\V7\aDgnV7.ma"
        File /oname=$vMSTNJFolder\mdlapps\aPickTool.ma  "Backup\V7\aPickTool.ma"
        File /oname=$vMSTNJFolder\mdlsys\required\TagEditor.ma "Backup\V7\TagEditor.ma"     ; 2014.04.04 added by humkyung
    ${EndIf}
    
    ; copy V8 ma files file to mdlapps folder
    ${If} $vMSTNV8iFolder != ""
        File /oname=$vMSTNV8iFolder\mdlapps\aDgnWriter.ma  "Backup\V8\aDgnWriter.ma"
        File /oname=$vMSTNV8iFolder\mdlapps\aPickTool.ma   "Backup\V8\aPickTool.ma"
    ${EndIf}
FunctionEnd

;-------------------------------------------------------------------------------------------------------------------------------------------
; Install Visual Studio Redistributables 2008+ SP1
; 2013.06.10
Function InstallVC2008Redist
   Push $R0
   ClearErrors
   ; check Redistributables is installed on x64
   ReadRegDword $R0 HKLM "SOFTWARE\Wow6432Node\Microsoft\Windows\CurrentVersion\Uninstall\{887868A2-D6DE-3255-AA92-AA0B5A59B874}" "Version"
   IfErrors 0 VSRedistInstalled
   ; check Redistributables is installed on x86
   ReadRegDword $R0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{887868A2-D6DE-3255-AA92-AA0B5A59B874}" "Version"
   ; if VS 2008+ redist SP1 not installed, install it
   IfErrors 0 VSRedistInstalled
   IfFileExists "$INSTDIR\Temp\vcredist_vs2008_x86_sp1.exe"  0 VSRedistInstalled
       ExecWait "$INSTDIR\Temp\vcredist_vs2008_x86_sp1.exe"
   Delete "$INSTDIR\Temp\vcredist_vs2008_x86_sp1.exe"

VSRedistInstalled:
   Pop $R0
FunctionEnd

; Install .NET Framework3.5 - 2014.04.18 added by humkyung
Function InstallNETFramework35
  Push $R0
  ClearErrors

  ;check .NET 3.5 is installed
  ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\NET Framework Setup\NDP\v3.5" "Version"
  IfErrors 0 NETFramework35Installed
  IfFileExists "$TEMP\dotnetfx35setup.exe" 0 NETFramework35Installed
        ExecWait "$TEMP\dotnetfx35setup.exe"
  Delete "$TEMP\dotnetfx35setup.exe"

NETFramework35Installed:
  Pop $R0
FunctionEnd
; up to here

; Install .NET Framework4 - 2014.03.31 added by humkyung
Function InstallNETFramework40
  Push $R0
  ClearErrors

  ;check Redistributables is installed on x64
  ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\NET Framework Setup\NDP\v4\Full" "Version"
  IfErrors 0 NETFramework40Installed
  IfFileExists "$TEMP\dotNetFx40_Full_setup.exe" 0 NETFramework40Installed
        ExecWait "$TEMP\dotNetFx40_Full_setup.exe"
  Delete "$TEMP\dotNetFx40_Full_setup.exe"

NETFramework40Installed:
  Pop $R0
FunctionEnd
; up to here
;-------------------------------------------------------------------------------------------------------------------------------------------

Section "SmartDraw_PDS" SEC01
  SectionIn RO              ; 필수 설치 요소
  SetShellVarContext all    ; for all users
  
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "SmartDraw.exe"

  CreateDirectory "$SMPROGRAMS\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk" "$INSTDIR\SmartDraw.exe"
  CreateShortCut "$DESKTOP\${PRODUCT_NAME}.lnk" "$INSTDIR\SmartDraw.exe"

  File "SmartDraw.exe"
  File "ExtractDatabase.exe"
  File "DataExchange.exe"
  File "SmartDrawModule_vc90.dll"
  File "SmartDrawAnno_vc90.dll"
  File "SmartDrawPDSModule_vc90.dll"
  File "SmartHSR_vc90.dll"
  File "aBaseTool_vc90.dll"
  File "aMesh2d_vc90.dll"
  File "SmartExchanger_vc90.dll"
  File "DgnLib_vc90.dll"
  File "SmartSolid_vc90.dll"
  File "aImage_vc90.dll"
  File "SmartDrawRsc_vc90.dll"
  File "is_vc90.dll"
  File "isutil_vc90.dll"
  File "isgui_vc90.dll"                  ;2011.08.06 added by humkyung
  File "DgnV7Lib_vc90.dll"
  File "BugTrap.dll"                     ;2011.08.19 added by humkyung
  File "vftlib_vc90.dll"

  File "SmartDrawFramework.dll"         ;2012.10.05 added by humkyung
  
; DLL for Excel
  File "GemBox.Spreadsheet.dll"
  
; DLL for Oracle
  File "oci.dll"
  File "ocijdbc11.dll"
  File "ociw32.dll"
  File "orannzsbb11.dll"
  File "oraocci11.dll"
  File "oraociei11.dll"
  File "oraociicus11.dll"
  File "orasql11.dll"

; DLL for Sqlite
  File "soci_3_2\soci_core_3_2.dll"
  File "soci_3_2\soci_sqlite3_3_2.dll"
  File "sqlite3.dll"
  File "SQLite.Interop.dll"
  File "System.Data.SQLite.dll"
  File "System.Data.SQLite.Linq.dll"
  
  File "readme.txt"

  SetOutPath "$INSTDIR\Setting"
  File "Setting\*.*"
 
  SetOutPath "$INSTDIR\Setting\Fonts\AutoCAD"
  File "Setting\Fonts\AutoCAD\*.vft"
  SetOutPath "$INSTDIR\Setting\Fonts\MSTN"
  File "Setting\Fonts\MSTN\*.vft"
  
  SetOutPath "$INSTDIR\Backup"
  File "Backup\*.*"
  SetOutPath "$APPDATA\SmartDraw_PDS\Drawings"
  File "Backup\Drawings\*.*"
  SetOutPath "$APPDATA\SmartDraw_PDS\Seed"
  File "Backup\Seed\*.*"

  SetOutPath "$APPDATA\Autodesk\ApplicationPlugins\SmartDraw\18"    ; AutoCAD2010
  File "Backup\AutoCAD\18\*.dll"
  SetOutPath "$APPDATA\Autodesk\ApplicationPlugins\SmartDraw\19"    ; AutoCAD2014
  File "Backup\AutoCAD\19\*.dll"
  
  SetOutPath "$INSTDIR\Resource"
  File "Resource\*.*"

  SetOutPath "$INSTDIR\Drawings"
  File "Drawings\PipingDrawing_vc90.dll"
  
  SetOutPath "$TEMP"
  File "Temp\vcredist_vs2008_x86_sp1.exe"   ; 2013.06.14 added by humkyung
  File "Temp\dotnetfx35setup.exe"           ; 2014.04.18 added by humkyung
  ;  File "Temp\dotNetFx40_Full_setup.exe"  ; 2014.03.31 added by humkyung
SectionEnd

Section "AutoUp" SEC02
  SectionIn RO   ; 필수 설치 요소

  SetOutPath "$INSTDIR"
  File ".\mfc90.dll"
  File ".\msvcp90.dll"
  File ".\msvcr90.dll"
  File "AutoUp_vc90.dll"
  File "SmartLMSLib_vc90.dll"    ; 2013.11.16 added by humkyung
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

; 설치하고 난 후...
Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\SmartDraw.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "Version" "${PRODUCT_VERSION}"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}\License" "IP" "175.126.145.37"
  WriteRegDWORD HKLM "${PRODUCT_DIR_REGKEY}\License" "Port" "2002"
  
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\SmartDraw.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  
  call InstallVC2008Redist
  call InstallNETFramework35
  
  ExecShell "open" "$INSTDIR\readme.txt"
SectionEnd

Function .onInit
         !insertmacro MUI_LANGDLL_DISPLAY
         ReadRegStr $R0 HKLM "${PRODUCT_UNINST_KEY}" "UninstallString"
         StrCmp $R0 "" done

         MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \
                    "${PRODUCT_NAME} is already installed. $\n$\nClick `OK` to remove the \
                    previous version or `Cancel` to cancel this upgrade." \
                    IDOK uninst
                    Abort done

;Run the uninstaller
uninst:
       ClearErrors
       ExecWait '"$R0" _?=$INSTDIR' ;Do not copy the uninstaller to a temp file
       ;abort          ; 프로그램 종료

       IfErrors no_remove_uninstaller
    ;You can either use Delete /REBOOTOK in the uninstaller or add some code
    ;here to remove the uninstaller. Use a registry key to check
    ;whether the user has chosen to uninstall. If you are using an uninstaller
    ;components page, make sure all sections are uninstalled.
  no_remove_uninstaller:

done:
FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name)는(은) 완전히 제거되었습니다."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "$(^Name)을(를) 제거하시겠습니까?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"

  Delete "$INSTDIR\*.*"

  Delete "$INSTDIR\Setting\Fonts\AutoCAD\*.vft"
  RMDir  "$INSTDIR\Setting\Fonts\AutoCAD"
  Delete "$INSTDIR\Setting\Fonts\MSTN\*.vft"
  RMDir  "$INSTDIR\Setting\Fonts\MSTN"
  RMDir  "$INSTDIR\Setting\Fonts"
  
  Delete "$INSTDIR\Setting\*.TBL"
  Delete "$INSTDIR\Setting\*.PAL"
  Delete "$INSTDIR\Setting\*.dat"
  Delete "$INSTDIR\Setting\NozzleChart.ini"
  Delete "$INSTDIR\Setting\SmartDraw_Project.ini"
  Delete "$INSTDIR\Setting\Piping.ini"
  Delete "$INSTDIR\Setting\CableTray.ini"
  Delete "$INSTDIR\Setting\OpeningHole.ini"
    
  Delete "$INSTDIR\Backup\*.*"
  RMDir  "$INSTDIR\Backup"

  Delete "$INSTDIR\Drawings\*.*"
  RMDir  "$INSTDIR\Drawings"

  Delete "$INSTDIR\Resource\*.*"
  RMDir  "$INSTDIR\Resource"
  
  Delete "$INSTDIR\Temp\*.*"
  RMDir  "$INSTDIR\Temp"
  
  RMDir "$INSTDIR"

  Delete "$DESKTOP\${PRODUCT_NAME}.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}\Website.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_PUBLISHER}\${PRODUCT_NAME}\Uninstall.lnk"
  RMDir "$SMPROGRAMS\${PRODUCT_NAME}"
  
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd
